apply plugin: 'com.android.application'

try {
    project.ext.keystore = System.env.POWERHOUR_KEYSTORE
    project.ext.keystoreAlias = System.env.POWERHOUR_ALIAS
    project.ext.keystorePassphrase = "keyring get google-play ${file(System.env.POWERHOUR_KEYSTORE).name}".execute().text.trim()
} catch(Exception ignored) {
    // IDEA has issues sync'ing gradle files with 'exec' so catch exception and fill with garbage until this is fixed
    project.ext.keystore = '/dev/null'
    project.ext.keystoreAlias = ''
    project.ext.keystorePassphrase = ''
}

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
}

android {
    compileSdkVersion 17
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "gilday.android.powerhour"
        minSdkVersion 8
        targetSdkVersion 17
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassphrase
            keyAlias keystoreAlias
            keyPassword keystorePassphrase
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}
